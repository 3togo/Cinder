apply plugin: 'com.android.application'
apply plugin: org.libcinder.gradle.CinderAppBuildPlugin

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    println "PROJECT DIR: ${projectDir}"
    println "BUILD DIR  : ${buildDir}"

    defaultConfig {
        applicationId "org.libcinder.samples.basicapp"
        minSdkVersion 19
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jni.srcDirs = ['src/main/jni', '../../../src', '../../../include']
            jniLibs.srcDirs = ['src/main/jniLibs', '../../../../../lib']
            assets.srcDirs = ['src/main/assets', '../../../assets']
        }

    }

    task disableDefaultNdkBuild() {
        doFirst {
            tasks.each {
                if(("compileDebugNdk" ==  it.name) || ("compileReleaseNdk" ==  it.name)) {
                    it.enabled = false
                }
            }
        }
    }

    def prebuildTask = tasks.getByPath( ":app:preBuild" )
    if( prebuildTask ) {
        prebuildTask.dependsOn disableDefaultNdkBuild
    }

    cinder {
        ndkDir = "${projectDir}/../../../../../../android-ndk-r10d"
        verbose = true
        moduleName = "BasicApp"
        srcFiles = ["../../../src/BasicApp.cpp"]
        cppFlags = "-std=c++11 -fexceptions -g"
        includeDirs = ["../../../../../include", "../../../../../boost"]
        ldLibs = ["log", "android", "EGL", "GLESv3", "z"]
        staticLibs = [
                "${projectDir}/../../../../../lib/android/\$(TARGET_PLATFORM)/\$(TARGET_ARCH_ABI)/libcinder_d.a",
                "${projectDir}/../../../../../lib/android/\$(TARGET_PLATFORM)/\$(TARGET_ARCH_ABI)/libboost_filesystem.a",
                "${projectDir}/../../../../../lib/android/\$(TARGET_PLATFORM)/\$(TARGET_ARCH_ABI)/libboost_system.a"
        ]
        stl = "gnustl_static"
        toolChainVersion = "4.9"
        archs = ["armeabi-v7a"]
        //archs = ["armeabi", "armeabi-v7a", "arm64-v8a", "mips", "mips64", "x86","x86_64"]
    }

    task ndkBuild(dependsOn: 'cinderCompileNdk') {
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
