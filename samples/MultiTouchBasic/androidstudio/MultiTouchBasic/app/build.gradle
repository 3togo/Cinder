apply plugin: 'com.android.application'
apply plugin: org.libcinder.gradle.CinderAppBuildPlugin

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    println "PROJECT DIR: ${projectDir}"
    println "BUILD DIR  : ${buildDir}"

    defaultConfig {
        applicationId "org.libcinder.samples.multitouchbasic"
        minSdkVersion 19
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jni.srcDirs = ['src/main/jni', '../../../src', '../../../include']
            jniLibs.srcDirs = ['src/main/jniLibs', '../../../../../lib']
            assets.srcDirs = ['src/main/assets', '../../../assets']
        }
    }

    task disableDefaultNdkBuild() {
        doFirst {
            tasks.each {
                if(("compileDebugNdk" ==  it.name) || ("compileReleaseNdk" ==  it.name)) {
                    it.enabled = false
                }
            }
        }
    }

    def prebuildTask = tasks.getByPath( ":app:preBuild" )
    if( prebuildTask ) {
        prebuildTask.dependsOn disableDefaultNdkBuild
    }

    cinder {
        ndkDir = "${projectDir}/../../../../../../android-ndk-r10d"
        cinderDir = "${projectDir}/../../../../.."
        verbose = false
        moduleName = "MultiTouchApp"
        srcFiles = ["../../../src/MultiTouchApp.cpp"]
        cppFlags = "-std=c++11 -fexceptions -g -mfpu=neon"
        includeDirs = ["${cinderDir}/include", "${cinderDir}/boost"]
        ldLibs = ["log", "android", "EGL", "GLESv3", "z"]
        staticLibs = [
                "${cinderDir}/lib/android/\$(TARGET_PLATFORM)/\$(TARGET_ARCH_ABI)/libcinder_d.a",
                "${cinderDir}/lib/android/\$(TARGET_PLATFORM)/\$(TARGET_ARCH_ABI)/libboost_filesystem.a",
                "${cinderDir}/lib/android/\$(TARGET_PLATFORM)/\$(TARGET_ARCH_ABI)/libboost_system.a"
        ]
        stl = "gnustl_static"
        toolChainVersion = "4.9"
        archs = ["armeabi-v7a"]
        //archs = ["armeabi", "armeabi-v7a", "arm64-v8a", "mips", "mips64", "x86","x86_64"]
    }

    task ndkBuildDebug(dependsOn: 'cinderCompileDebugNdk') {}
    task ndkBuildRelease(dependsOn: 'cinderCompileReleaseNdk') {}

    tasks.withType(JavaCompile) {
        compileTask ->
            if("compileDebugJava" == compileTask.name) {
                compileTask.dependsOn ndkBuildDebug
            }
            else if("compileReleaseJava" == compileTask.name) {
                compileTask.dependsOn ndkBuildRelease
            }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
